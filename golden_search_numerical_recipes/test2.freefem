//description: looks for eigenvalues of backward Kolmogorov operator (with quadratic potential) in L^2(\psi_\infty})
//version for golden search

//************************
//ALWAYS KEEP THIS WHEN RUNNING FROM C++
verbosity = 0;
string astring = ARGV[5];
//************************

real a = atof(astring);

for(int i=0;i<ARGV.n;++i)
   {
     cout << ARGV[i] << endl;
   }
   
//mesh data
 real r =5;
 real xlow=-r,xhigh=r;
 real ylow=-r,yhigh=r;
 int res = 100;

//problem data: potential V and perturbation b

// INPUT: $V, \Nabla (V), \Delta (V)$
 real s11=1., s12=0.;
 real s21=0., s22=0.5; 
 func V = 0.5*(s11*x*x + (s12+s21)*x*y +s22*y*y);
 func dxV = s11*x + s12*y;
 func dyV = s21*x + s22*y;

// INPUT: \rho-div-free perturbation b
func b1 = a * dyV;
func b2 = -a * dxV;

//invariant measere (not to be changed)
 func rho = exp(-V(x,y));
 func dxrho = - rho(x,y) * dxV(x,y);
 func dyrho = - rho(x,y) * dyV(x,y);

// eigenproblem parameters
real sigma = 0.;	//shift
int nev=10;		//number of eigenvalues to compute close to sigma

//Mesh generation
 //square mesh
  int n=res, m=res;
  mesh Th=square(n,m,[xlow+(xhigh-xlow)*x,ylow+(yhigh-ylow)*y]);

fespace Vh(Th,P1);
Vh u1,u2;
varf op(u1,u2) =- int2d(Th)((dx(u1)*dx(u2)+dy(u1)*dy(u2))*rho)
     	        - int2d(Th)((dx(u1)*dxrho+dy(u1)*dyrho)*u2)
     	      	- int2d(Th)(((dxV)*dx(u1)+(dyV)*dy(u1))*u2*rho)
		+ int2d(Th)(((b1)*dx(u1)+(b2)*dy(u1))*u2*rho)
     	       	- int2d(Th)( sigma* u1*u2*rho );		      //OP=A_sigma B, shifted matrix

varf b([u1],[u2]) = int2d(Th)( u1*u2*rho );	  //no BC (!)
matrix OP= op(Vh,Vh,solver=UMFPACK);
matrix B= b(Vh,Vh,solver=UMFPACK);		  

real[int] ev(nev);		//to store eigenvalue
real[int] iev(nev);		//to store imaginary part of eigenvalues
Vh[int] eV(nev);		//to store eigenvectors

//resolution of dicrete eigenvalue problem
int k=EigenValue(OP,B,sym=false,sigma=sigma,value=ev,ivalue=iev, vector=eV,
		 tol=1e-10,maxit=0,ncv=0);

//print results
/*
for (int i=0;i<k;i++){
    u1 = eV[i];
    cout << " ---- " << i+1 << " "		
    	 << ev[i] << "+i" << iev[i]	//eigenvalue
    	 << " err= " << - int2d(Th)((dx(u1)*dx(u1)+dy(u1)*dy(u1))*rho)
     	        	- int2d(Th)((dx(u1)*dxrho+dy(u1)*dyrho)*u1)
     	      		- int2d(Th)(((dxV-b1)*dx(u1)+(dyV-b2)*dy(u1))*u1*rho)		     
     	       		- int2d(Th)( ev[i]*u1*u1*rho )
	<< " --- " << endl;
    plot(eV[i],cmm= i+1 + "th eigenvalue=" + ev[i] + "+i" + iev[i] ,wait=1, dim=3,fill=1, value=1);
}
*/
string tag = "###BEGIN_RESULT###";
//cout << tag << ev[1] << endl;
cout << tag << sqrt(ev[1]*ev[1]+iev[1]*iev[1]) << endl;
